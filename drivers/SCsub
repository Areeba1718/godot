#!/usr/bin/env python

from typing import TYPE_CHECKING

from SCons.Script.SConscript import SConsEnvironment

if TYPE_CHECKING:
    env = SConsEnvironment()
    Import = SConsEnvironment.Import

Import("env")

env.drivers_sources = []

# OS drivers
env.SConscript("unix/SCsub")
env.SConscript("windows/SCsub")

# Sounds drivers
env.SConscript("alsa/SCsub")
env.SConscript("coreaudio/SCsub")
env.SConscript("pulseaudio/SCsub")
if env["platform"] == "windows":
    env.SConscript("wasapi/SCsub")
    if not env.msvc:
        env.SConscript("backtrace/SCsub")
if env["xaudio2"]:
    env.SConscript("xaudio2/SCsub")

# Midi drivers
env.SConscript("alsamidi/SCsub")
env.SConscript("coremidi/SCsub")
env.SConscript("winmidi/SCsub")

# Graphics drivers
if env["vulkan"]:
    env.SConscript("vulkan/SCsub")
if env["d3d12"]:
    env.SConscript("d3d12/SCsub")
if env["opengl3"]:
    env.SConscript("gl_context/SCsub")
    env.SConscript("gles3/SCsub")
    env.SConscript("egl/SCsub")

# Core dependencies
env.SConscript("png/SCsub")

env.add_source_files(env.drivers_sources, "*.cpp")

lib = env.add_library("drivers", env.drivers_sources)
env.Prepend(LIBS=[lib])
