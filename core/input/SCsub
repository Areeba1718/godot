#!/usr/bin/env python

Import("env")

from platform_methods import run_in_subprocess
import input_builders


# Order matters here. Higher index controller database files write on top of lower index database files.
controller_databases = [
    "#core/input/gamecontrollerdb_204.txt",
    "#core/input/gamecontrollerdb_205.txt",
    "#core/input/gamecontrollerdb.txt",
    "#core/input/godotcontrollerdb.txt",
]

env.Depends("#core/input/default_controller_mappings.gen.cpp", controller_databases)
env.CommandNoCache(
    "#core/input/default_controller_mappings.gen.cpp",
    controller_databases,
    run_in_subprocess(input_builders.make_default_controller_mappings),
)

local_sources = env.glob_files('*.cpp') + env.glob_files('*.h')
env.add_project_sources(local_sources)
env.add_objects_from_sources(env.core_objects, local_sources)

# Don't warn about duplicate entry here, we need it registered manually for first build,
# even if later builds will pick it up twice due to above *.cpp globbing.
mappings_gen_files = ["#core/input/default_controller_mappings.gen.cpp"]
env.add_project_sources(mappings_gen_files)
env.add_objects_from_sources(env.core_objects, mappings_gen_files, warn_duplicates=False)
