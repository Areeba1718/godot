#!/usr/bin/env python

from typing import TYPE_CHECKING

import input_builders
from SCons.Script.SConscript import SConsEnvironment

if TYPE_CHECKING:
    env = SConsEnvironment()
    Import = SConsEnvironment.Import

Import("env")

# Order matters here. Higher index controller database files write on top of lower index database files.
controller_databases = [
    "gamecontrollerdb.txt",
    "godotcontrollerdb.txt",
]

gensource = env.CommandNoCache(
    "default_controller_mappings.gen.cpp",
    controller_databases,
    env.Run(input_builders.make_default_controller_mappings),
)

env.add_source_files(env.core_sources, "*.cpp")
env.add_source_files(env.core_sources, gensource)
