#!/usr/bin/env python

Import("env")

env_crypto = env.Clone()

is_builtin = env["builtin_mbedtls"]
has_module = env["module_mbedtls_enabled"]

if is_builtin or not has_module:
    # Use our headers for builtin or if the module is not going to be compiled.
    # We decided not to depend on system mbedtls just for these few files that can
    # be easily extracted.
    env_crypto.Prepend(CPPPATH=["#thirdparty/mbedtls/include"])

# MbedTLS core functions (for CryptoCore).
# If the mbedtls module is compiled we don't need to add the .c files with our
# custom config since they will be built by the module itself.
# Only if the module is not enabled, we must compile here the required sources
# to make a "light" build with only the necessary mbedtls files.
if not has_module:
    env_thirdparty = env_crypto.Clone()
    env_thirdparty.disable_warnings()
    # Custom config file
    env_thirdparty.Append(
        CPPDEFINES=[("MBEDTLS_CONFIG_FILE", '\\"thirdparty/mbedtls/include/godot_core_mbedtls_config.h\\"')]
    )
    thirdparty_mbedtls_dir = "#thirdparty/mbedtls/library/"
    thirdparty_mbedtls_sources = [
        "aes.h"
        "aes.c",
        "base64.h"
        "base64.c",
        "md5.h",
        "md5.c",
        "sha1.h",
        "sha1.c",
        "sha256.h",
        "sha256.c",
        "godot_core_mbedtls_platform.h",
        "godot_core_mbedtls_platform.c",
    ]
    thirdparty_mbedtls_sources = [thirdparty_mbedtls_dir + file for file in thirdparty_mbedtls_sources]
    env.add_project_sources(thirdparty_mbedtls_sources)
    env.add_objects_from_sources(env.core_sources, thirdparty_mbedtls_sources)


local_sources = env.glob_files('*.h') + env.glob_files('*.cpp')
env.add_project_sources(local_sources)
env_crypto.add_objects_from_sources(env.core_objects, local_sources)
