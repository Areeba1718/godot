#!/usr/bin/env python

from typing import TYPE_CHECKING

from SCons.Script.SConscript import SConsEnvironment

if TYPE_CHECKING:
    env = SConsEnvironment()
    env_modules = SConsEnvironment()
    Import = SConsEnvironment.Import

Import("env")
Import("env_modules")

env_minimp3 = env_modules.Clone()

thirdparty_dir = "#thirdparty/minimp3/"

# Treat minimp3 headers as system headers to avoid raising warnings. Not supported on MSVC.
if not env.msvc:
    env_minimp3.Append(CPPFLAGS=["-isystem", env.Dir(thirdparty_dir).path])
else:
    env_minimp3.Prepend(CPPPATH=[thirdparty_dir])

if not env["minimp3_extra_formats"]:
    env_minimp3.Append(CPPDEFINES=["MINIMP3_ONLY_MP3"])

# Godot source files
env_minimp3.add_source_files(env.modules_sources, "*.cpp")
