#!/usr/bin/env python

Import('env')
Import('env_modules')

env_pvr = env_modules.Clone()

# Thirdparty source files
# Not unbundled so far since not widespread as shared library
thirdparty_dir = "#thirdparty/pvrtccompressor/"
thirdparty_sources = [
    "BitScale.cpp",
    "MortonTable.cpp",
    "PvrTcDecoder.cpp",
    "PvrTcEncoder.cpp",
    "PvrTcPacket.cpp",
]
thirdparty_sources = [thirdparty_dir + file for file in thirdparty_sources]

env_pvr.add_source_files(env.modules_sources, thirdparty_sources)
env_pvr.Append(CPPPATH=[thirdparty_dir])

# Godot source files
env_pvr.add_source_files(env.modules_sources, "*.cpp")

# Msvc Intellisense needs to know pvr is available, pass to main env
if "platform" in env and env["platform"] == "windows":
    env.Append(CPPPATH=env_pvr['CPPPATH'])
    env.Append(CPPFLAGS=env_pvr['CPPFLAGS'])

Export('env')