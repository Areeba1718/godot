#!/usr/bin/env python

Import('env')

import os
from platform_methods import run_in_subprocess
import platform_unix_builders

sources = [
    "crash_handler_unix.cpp",
    "detect_prime.cpp",
    "godot_unix.cpp",
    "joypad_linux.cpp",
    "os_genericunix.cpp",
    "power_unix.cpp",
]

if env['x11']:
    sources += [
        "x11/context_gl_x11.cpp",
        "x11/key_mapping_x11.cpp",
        "x11/os_x11.cpp",
    ]
if env['wayland']:
    env = env.Clone(tools=['wayland-scanner'], toolpath=['tools'])
    protos = [
        "xdg-shell",
        "pointer-constraints-unstable-v1",
    ]
    proto_srcs = []
    for proto in protos:
        xml = "wayland/protocol/%s.xml" % proto
        header = "wayland/protocol/%s.h" % proto
        code = "wayland/protocol/%s.c" % proto
        env.WaylandScanner("client-header", header, xml)
        env.WaylandScanner("private-code", code, xml)
        proto_srcs.append(code)
    sources += [
        "wayland/context_egl_wayland.cpp",
        "wayland/key_mapping_xkb.cpp",
        "wayland/os_wayland.cpp",
    ] + proto_srcs

prog = env.add_program('#bin/godot', sources)

if (env["debug_symbols"] == "full" or env["debug_symbols"] == "yes") and env["separate_debug_symbols"]:
    env.AddPostAction(prog, run_in_subprocess(
        platform_unix_builders.make_debug_unix))
