#!/usr/bin/env python

Import("env")

tvos_lib = [
    "godot_appletv.mm",
    "os_appletv.mm",
    "main.m",
    "app_delegate.mm",
    "view_controller.mm",
    "tvos.mm",
    "joypad_appletv.mm",
    "godot_view.mm",
    "display_layer.mm",
    "godot_app_delegate.m",
    "godot_view_renderer.mm",
    "keyboard_input_view.mm",
    "native_video_view.m",
]

env_tvos = env.Clone()
tvos_lib = env_tvos.add_library("tvos", tvos_lib)

# (tvOS) Enable module support
env_tvos.Append(CCFLAGS=["-fmodules", "-fcxx-modules"])


def combine_libs(target=None, source=None, env=None):
    lib_path = target[0].srcnode().abspath
    if "osxcross" in env:
        libtool = "$TVOSPATH/usr/bin/${tvos_triple}libtool"
    else:
        libtool = "$TVOSPATH/usr/bin/libtool"
    env.Execute(
        libtool + ' -static -o "' + lib_path + '" ' + " ".join([('"' + lib.srcnode().abspath + '"') for lib in source])
    )


combine_command = env_tvos.Command("#bin/libgodot" + env_tvos["LIBSUFFIX"], [tvos_lib] + env_tvos["LIBS"], combine_libs)
