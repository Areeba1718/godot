#!/usr/bin/env python

from typing import TYPE_CHECKING

from SCons.Script.SConscript import SConsEnvironment

if TYPE_CHECKING:
    env = SConsEnvironment()
    Import = SConsEnvironment.Import

Import("env")

if "RD_GLSL" in env["BUILDERS"]:
    # find all include files
    gl_include_files = [str(f) for f in env.Glob("*_inc.glsl")]

    # find all shader code(all glsl files excluding our include files)
    glsl_files = [str(f) for f in env.Glob("*.glsl") if str(f) not in gl_include_files]

    # make sure we recompile shaders if include files change
    env.Depends([f + ".gen.h" for f in glsl_files], gl_include_files + ["#glsl_builders.py"])

    # compile shaders
    for glsl_file in glsl_files:
        env.RD_GLSL(glsl_file)

env.SConscript("effects/SCsub")
env.SConscript("environment/SCsub")
env.SConscript("forward_clustered/SCsub")
env.SConscript("forward_mobile/SCsub")
