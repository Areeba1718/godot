#!/usr/bin/env python

import os
from typing import TYPE_CHECKING

import editor_icons_builders
from SCons.Script.SConscript import SConsEnvironment

if TYPE_CHECKING:
    env = SConsEnvironment()
    Import = SConsEnvironment.Import

Import("env")


env["BUILDERS"]["MakeEditorIconsBuilder"] = env.Builder(
    action=env.Run(editor_icons_builders.make_editor_icons_action),
    suffix=".h",
    src_suffix=".svg",
)

# Editor's own icons
icon_sources = env.Glob("*.svg")

# Module icons
for path in env.module_icons_paths:
    if not os.path.isabs(path):
        icon_sources += env.Glob("#" + path + "/*.svg")  # Built-in.
    else:
        icon_sources += env.Glob(path + "/*.svg")  # Custom.

env.Alias("editor_icons", [env.MakeEditorIconsBuilder("#editor/themes/editor_icons.gen.h", icon_sources)])
